program = { SOI ~ decl* ~ EOI }

decl = { task | extern_task | struct }

extern_task = { "extern" ~ "task" ~ (#name = ident) ~ "(" ~ (param_list | #variadic = variadic_params)? ~ ")" ~ ("->" ~ #returns = type)? ~ ";" }

variadic_params = { "..." }

task = { "task" ~ (#name = ident) ~ "(" ~ param_list? ~ ")" ~ ("->" ~ #returns = type)? ~ "{" ~ (#body = statement)* ~ "}" }

struct = { "struct" ~ (#name = ident) ~ "{" ~ param_list ~ "}" }

param_list = _{ (#param = ident ~ ":" ~ #type = type ~ ",")* ~ (#param = ident ~ ":" ~ #type = type) }

ident = @{ (ASCII_ALPHA | "_")+ ~ (ASCII_ALPHANUMERIC | "_")* }

type = { #bool = bool | #int = integer | #array = array | #ptr = pointer_to | #named = ident }
bool = { "bool" }
pointer_to = { "&" ~ type }
array = { "[" ~ #item = type ~ ";" ~ (#len = int_literal) ~ "]" }
int_literal = { ASCII_DIGIT+ }
integer = @{ ("u" | "i") ~ ("8" | "16" | "32" | "64") }

statement = { #cond = conditional | (#var_decl = var_decl | #var_assign = var_assign | #ptr_assign = ptr_assign | (#return = return) | #sched = ("schedule" ~ call) | #exit = exit | (#expr = expression)) ~ ";" }
exit = { "exit" }
return = { "return" ~ expression? }
var_decl = { "var " ~ #name = ident ~ ":" ~ #type = type ~ "=" ~ #value = expression }
var_assign = { #name = ident ~ "=" ~ #value = expression }
ptr_assign = { "*" ~ #ptr = ident ~ "=" ~ #value = expression }

conditional = { "if" ~ (#test = expression) ~ "{" ~ #then = statement* ~ "}" ~ ("else" ~ (("{" ~ #else = statement* ~ "}") | conditional))? }

expression = { equality }

equality = { comparison ~ (eq_op ~ comparison)* }
eq_op = {"==" | "!="}

comparison = { term ~ (cmp_op ~ term)* }
cmp_op = { ">" | ">=" | "<" | "<=" | "&&" | "||"}

term = { factor ~ (add_op ~ factor)* }
add_op = {"-" | "+"}

factor = { unary ~ (mul_op ~ unary)* }
mul_op = { "/" | "*" }

unary = { un_op ~ unary | primary }
un_op = { "!" | "-" | "*" | "&" }

primary = { call | value | ident | "(" ~ expression ~ ")" }

call = { #task = ident ~ "(" ~ arg_list? ~ ")" }

arg_list = _{ (#arg = expression ~ ",")* ~ #arg = expression }

value = { string | #int = ASCII_DIGIT+ | #bool = ("true" | "false") | "'" ~ #char = (ASCII) ~ "'" }

string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

WHITESPACE = _{ WHITE_SPACE | NEWLINE }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }