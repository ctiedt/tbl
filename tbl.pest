program = { SOI ~ decl* ~ EOI }

decl = { task | extern_task | struct }

extern_task = { "extern" ~ "task" ~ (#name = ident) ~ "(" ~ (param_list | #variadic = variadic_params)? ~ ")" ~ ("->" ~ #returns = type)? ~ ";" }

variadic_params = { "..." }

task = { "task" ~ (#name = ident) ~ "(" ~ param_list? ~ ")" ~ ("->" ~ #returns = type)? ~ "{" ~ (#body = statement)* ~ "}" }

struct = { "struct" ~ (#name = ident) ~ "{" ~ param_list ~ "}" }

param_list = _{ (#param = ident ~ ":" ~ #type = type ~ ",")* ~ (#param = ident ~ ":" ~ #type = type) }

ident = @{ (ASCII_ALPHA | "_")+ ~ (ASCII_ALPHANUMERIC | "_")* }

type = { #task_ptr = task_ptr | #bool = bool | #int = integer | #array = array | #ptr = pointer_to | #named = ident }
bool = { "bool" }
pointer_to = { "&" ~ type }
array = { "[" ~ #item = type ~ ";" ~ (#len = int_literal) ~ "]" }
int_literal = { ASCII_DIGIT+ }
integer = @{ ("u" | "i") ~ ("8" | "16" | "32" | "64") }
task_ptr = {"task" ~ "(" ~ task_ptr_args? ~ ")" ~ ("->" ~ #returns = type)? }
task_ptr_args = _{ (#type = type ~ ",")* ~ (#type = type) }

statement = { #cond = conditional | (#var_decl = var_decl | #assign = assign | (#return = return) | #sched = ("schedule" ~ expression) | #exit = exit | (#expr = expression)) ~ ";" }
exit = { "exit" }
return = { "return" ~ expression? }
var_decl = { "var " ~ #name = ident ~ ":" ~ #type = type ~ "=" ~ #value = expression }
assign = { #location = expression ~ "=" ~ #value = expression}

conditional = { "if" ~ (#test = expression) ~ "{" ~ #then = statement* ~ "}" ~ ("else" ~ (("{" ~ #else = statement* ~ "}") | conditional))? }

expression = { equality }

equality = { comparison ~ (eq_op ~ comparison)* }
eq_op = {"==" | "!="}

comparison = { term ~ (cmp_op ~ term)* }
cmp_op = { ">" | ">=" | "<" | "<=" | "&&" | "||"}

term = { factor ~ (add_op ~ factor)* }
add_op = {"-" | "+"}

factor = { unary ~ (mul_op ~ unary)* }
mul_op = { "/" | "*" }

unary = { un_op ~ unary | postfix }
un_op = { "!" | "-" | "*" | "&" }

postfix = {  primary ~ postfix_op | primary }
postfix_op = _{ #struct_access = struct_access | #cast = cast | #call = call }
struct_access = { "." ~ #member = ident }
cast = { "as" ~ #to = type }
call = { "(" ~ arg_list? ~ ")" }

primary = { value | ident | "(" ~ expression ~ ")" }

arg_list = _{ (#arg = expression ~ ",")* ~ #arg = expression }

value = { #struct = struct_literal | string | #int = ASCII_DIGIT+ | #bool = ("true" | "false") | "'" ~ #char = (ASCII) ~ "'" }
struct_literal = { "{" ~ (#member = ident ~ ":" ~ #value = expression ~ ",")* ~ (#member = ident ~ ":" ~ #value = expression) ~ "}" }

string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

WHITESPACE = _{ WHITE_SPACE | NEWLINE }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }